// Author:  CÃ©sar Liedke
// Description: Follow Animal, Tame, Release and Kill
// Instructions:
//
// Set the skill cap for AnimalTaming
if skill 'animal taming' >= 100
  @setskill 'animal taming' 'locked'
  stop
endif
//
@removelist 'TamedPetName'
@removelist 'HowToKill'
@removelist 'Errors'
@removelist 'TamingTable'
//
if not listexists 'TamedPetName'
    // Set pushlist PetName to whatever you like
    createlist 'TamedPetName'
    pushlist 'TamedPetName' 'PetName'
endif
if not listexists 'HowToKill'
    //  Set to 'Melee', 'Ranged' or 'Magic'
    createlist 'HowToKill'
    pushlist 'HowToKill' 'Ranged'
endif
if not listexists 'Errors'
    createlist 'Errors'
endif
if not listexists 'Unstuck'
    createlist 'Unstuck'
endif
if not listexists 'TamingTable'
    createlist 'TamingTable'
endif
//
if list 'Errors' == 0
    pushlist 'Errors' 'too many owners'
    pushlist 'Errors' 'only be tamed by'
    pushlist 'Errors' 'no chance of taming'
    pushlist 'Errors' 'looks tame already'
    pushlist 'Errors' 'cannot be seen'
    //pushlist 'Errors' 'clear path to the animal'
endif
if list 'Unstuck' == 0
    pushlist 'Unstuck' 'east'
    pushlist 'Unstuck' 'west'
    pushlist 'Unstuck' 'south'
    pushlist 'Unstuck' 'north'
endif
if skill 'Animal Taming' < 60
    pushlist 'TamingTable' 0x5 // Eagle
    pushlist 'TamingTable' 0xd1 // Goat
    pushlist 'TamingTable' 0xcb // Pig
    pushlist 'TamingTable' 0x122 // Pig
    pushlist 'TamingTable' 0xed // Hind
    pushlist 'TamingTable' 0xe1 // Timber wolf
    pushlist 'TamingTable' 0xdc // Llama
    pushlist 'TamingTable' 0xcc // Horse
    pushlist 'TamingTable' 0xe2 // Horse
    pushlist 'TamingTable' 0xe4 // Horse
    pushlist 'TamingTable' 0xc8 // Horse
    pushlist 'TamingTable' 0x3e9f // Horse
    pushlist 'TamingTable' 0x3ea0 // Horse
    pushlist 'TamingTable' 0x3ea1 // Horse
    pushlist 'TamingTable' 0x3ea2 // Horse
    pushlist 'TamingTable' 0xe7 // Cow
    pushlist 'TamingTable' 0xd8 // Cow
    pushlist 'TamingTable' 0xa7 // Bear
    pushlist 'TamingTable' 0xd3 // Bear
    pushlist 'TamingTable' 0x3f // Cougar
elseif skill 'Animal Taming' < 75
    pushlist 'TamingTable' 0xd4 // Grizzly Bear
    pushlist 'TamingTable' 0x1b // Grey Wolf
    pushlist 'TamingTable' 0x19 // Grey Wolf
    pushlist 'TamingTable' 0x41 // Snow Leopard
    pushlist 'TamingTable' 0x40 // Snow Leopard
    pushlist 'TamingTable' 0xdd // Walrus
    pushlist 'TamingTable' 0xd5 // Polar Bear
    pushlist 'TamingTable' 0xea // Great Hart
elseif skill 'Animal Taming' < 95.1
    pushlist 'TamingTable' 0xf8 // Gaman
    pushlist 'TamingTable' 0xe8 // Bull
    pushlist 'TamingTable' 0xe9 // Bull
    pushlist 'TamingTable' 0xea // Great Hart
    pushlist 'TamingTable' 0x25 // White Wolf
    pushlist 'TamingTable' 0x22 // White Wolf
    pushlist 'TamingTable' 0x41 // Snow Leopard
    pushlist 'TamingTable' 0x40 // Snow Leopard
    pushlist 'TamingTable' 0xbb // Ridgeback
else
    pushlist 'TamingTable' 0xe8 // Bull
    pushlist 'TamingTable' 0xe9 // Bull
    pushlist 'TamingTable' 0x7a // Unicorn
    pushlist 'TamingTable' 0x84 // Ki-rin
endif
//
if not @findobject 'pet'
    for 0 to 'TamingTable'
        if @findtype 'TamingTable[]' 'any' 'ground'
            @settimer 'stuck' 0
            @setalias 'pet' 'found'
            break
        endif
  endfor
else
    // Pathfind
    @settimer 'pathfind' 0
    while @findobject 'pet' and not @inrange 'pet' 2
        if dead
            break
        elseif @timer 'pathfind' >= 5000
            // Diverge
            if direction == 0
                @poplist 'Unstuck' 'north'
            elseif direction == 2
                @poplist 'Unstuck' 'east'
            elseif direction == 4
                @poplist 'Unstuck' 'south'
            elseif direction == 6
                @poplist 'Unstuck' 'west'
            endif
            // Unstuck
            if list 'Unstuck' != 0
                for 5
                    run 'Unstuck[0]'
                    pause 100
                endfor
                poplist 'Unstuck' 'front'
            endif
            break
        elseif @x 'pet' > x 'self' and @y 'pet' > y 'self'
            run 'southeast'
        elseif @x 'pet' < x 'self' and @y 'pet' > y 'self'
            run 'southwest'
        elseif @x 'pet' > x 'self' and @y 'pet' < y 'self'
            run 'northeast'
        elseif @x 'pet' < x 'self' and @y 'pet' < y 'self'
            run 'northwest'
        elseif @x 'pet' > x 'self' and @y 'pet' == y 'self'
            run 'east'
        elseif @x 'pet' < x 'self' and @y 'pet' == y 'self'
            run 'west'
        elseif @x 'pet' == x 'self' and @y 'pet' > y 'self'
            run 'south'
        elseif @x 'pet' == x 'self' and @y 'pet' < y 'self'
            run 'north'
        endif
        pause 100
    endwhile
    if @inrange 'pet' 2
        // Tame
        @clearjournal
        if name 'pet' != 'trashpet'
            useskill 'Animal Taming'
            waitfortarget 5000
            target! 'pet'
        endif
        // Handle
        if @property '(tame)' 'pet'
            for 5
                if @gumpexists 0x909cc741 or not @findobject 'pet'
                    break
                elseif dead or followers == 0
                    break
                elseif @name 'pet' != 'trashpet'
                    rename 'pet' 'trashpet'
                else
                    msg 'trashpet release'
                endif
                pause 500
            endfor
            replygump 0x909cc741 2
            sysmsg 'Success: tamed and released!' 88
            //ignoreobject 'pet'
            //@unsetalias 'pet'
        elseif name 'pet' == 'trashpet' and not @property '(tame)' 'pet'
            sysmsg 'Attacking: ex-tamed pet' 33
            while hits 'pet' > 0
                // attack 'pet'
                if skill 'swordsmanship' >= 40
                    sysmsg 'Attacking with Swords: moving closer to attack' 33
                elseif skill 'fencing' >= 40
                    sysmsg 'Attacking with Fencing: moving closer to attack' 33
                elseif skill 'mace fighting' >= 40
                    sysmsg 'Attacking with Mace: moving closer to attack' 33
                elseif skill 'archery' >= 40
                    sysmsg 'Attacking: attacking from distance' 33
                elseif skill 'magery' >= 40
                    sysmsg 'Attacking: attacking with mystical forces' 33
                    cast 'magic arrow'
                    waitfortarget 5000
                    target! 'pet'
                    pause 1000
                elseif skill 'necromancy' >= 40
                    sysmsg 'Tocando o zaralho' 33
                endif
                pause 800
            endwhile
            ignoreobject 'pet'
            @unsetalias 'pet'
        else
            @settimer 'stuck' 0
            for 0 to 'Errors'
                if @injournal 'Errors[]'
                    sysmsg 'Ignore: error message!' 25
                    ignoreobject 'pet'
                    @unsetalias 'pet'
                    break
                endif
            endfor
        endif
    elseif @timer 'stuck' >= 20000
        sysmsg 'Ignore: stuck timeout!' 25
        ignoreobject 'pet'
        @unsetalias 'pet'
    endif
endif